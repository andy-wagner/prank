/**
 * Copyright 2012 David Millett
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 *  limitations under the License.
 */
plugins {
  id 'com.github.ben-manes.versions' version '0.20.0'
  id 'com.github.spotbugs' version '1.6.8'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'com.github.spotbugs'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'com.github.dmillett'
archivesBaseName = 'prank'
version = '1.2.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

build.dependsOn check

task prankJar(type: Jar)

task prankSourcesJar(type: Jar) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
  classifier = 'javadoc'
  from javadoc
}

artifacts {
  archives prankJar
  archives prankSourcesJar
  archives javadocJar
}

//task wrapper(type: Wrapper) {
//  gradleVersion = '4.10.3'
//}

repositories {
  mavenCentral()
}

dependencies {
  compile 'org.slf4j:slf4j-log4j12:1.7.25'

  testCompile 'junit:junit:4.12'
  testCompile 'org.mockito:mockito-all:1.10.19'
}

dependencyUpdates.resolutionStrategy = {
  componentSelection { rules ->
    rules.all { ComponentSelection selection ->
      boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
        selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
      }
      if (rejected) {
        selection.reject('Release candidate')
      }
    }
  }
}

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
    }
}

tasks.withType(FindBugs) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

// Required due to Gradle issue with SpotBugs
ext {
  SpotBugsTask = com.github.spotbugs.SpotBugsTask
}

spotbugs {
  toolVersion = '3.1.9'
}

tasks.withType(SpotBugsTask) {
  reports {
    xml.enabled = false
    html.enabled = true
  }
}

spotbugsTest.enabled = false

signing {
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

//      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
//        authentication(userName: ossrhUsername, password: ossrhPassword)
//      }
//
//      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
//        authentication(userName: ossrhUsername, password: ossrhPassword)
//      }

      pom.project {
        name 'prank'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'A Java parallel ranking/scoring library.'
        url 'https://github.com/dmillett/prank'

        scm {
          connection 'https://github.com/dmillett/prank.git'
          developerConnection 'https://github.com/dmillett/prank.git'
          url 'https://github.com/dmillett/prank'
        }

        licenses {
          license {
            name 'The Apache License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
          }
        }

        developers {
          developer {
            id 'dmillett'
            name 'dmillett'
            email 'd_millett@hotmail.com'
          }
        }
      }
    }
  }
}

jacocoTestReport.dependsOn test
check.dependsOn jacocoTestReport